buildscript {
    ext.cordentity_version = '5c8069a879051ef456cfd53f49b816677dfa9512'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.3.31'
    ext.spring_boot_version = '2.1.5.RELEASE'
    ext.kotlin_logging_version = '1.6.24'
    ext.h2_version = '1.4.199'
    ext.jna_version = '4.5.1'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

plugins {
    id "com.moowork.node" version "1.3.1"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'com.luxoft.ssi'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.sovrin.org/repository/maven-releases' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.github.hyperledger-labs.cordentity:cordentity-indy-utils:$cordentity_version"
    implementation "com.github.hyperledger-labs.cordentity:cordentity-agent-connection:$cordentity_version"
    implementation "net.java.dev.jna:jna:$jna_version"
    implementation "io.github.microutils:kotlin-logging:$kotlin_logging_version"

    implementation("com.h2database:h2:$h2_version")

    // Spring Boot
    implementation('org.springframework.boot:spring-boot-starter')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')


    testCompile group: 'junit', name: 'junit', version: junit_version
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.boot:spring-boot-test')
    testCompile('org.springframework.boot:spring-boot-starter-logging')
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task buildFrontend(type: NpmTask) {
    args = ["run", "build"]
}

buildFrontend.dependsOn(npmInstall)

node {
    version = "8.6.0"
    download = true
    npmWorkDir = file("frontend")
    nodeModulesDir = file("frontend")
}

bootJar {
    mainClassName = 'com.luxoft.ssi.web.WebApplicationKt'

    from('frontend/dist') {
        into 'static'
    }
}

bootJar.dependsOn(buildFrontend)
